////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ОБЪЕКТА

Процедура ОбработкаПроведения(Отказ, Режим)
	
	БазоваяЕдиницаИзмерения = Константы.БазоваяЕдиницаИзмерения.Получить();
	
	// Создание движений в регистре накопления ТоварныеЗапасы
	Движения.ТоварныеЗапасы.Записывать = Истина;
	Для каждого ТекСтрокаТовары Из Товары Цикл

		Движение = Движения.ТоварныеЗапасы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Товар = ТекСтрокаТовары.Товар;
		Движение.Склад = Склад;
		
		КоличествоТовара = ТекСтрокаТовары.Количество; 
		Если ТекСтрокаТовары.ЕдиницаИзмерения <> БазоваяЕдиницаИзмерения Тогда
			КоэффициентПерерасчета = КоэффициентПерерасчетаЕдиницыИзмеренияНоменклатуры(ТекСтрокаТовары.Товар,  ТекСтрокаТовары.ЕдиницаИзмерения);  
			Если КоэффициентПерерасчета <> 1 Тогда
				КоличествоТовара = КоличествоТовара * КоэффициентПерерасчета;
			КонецЕсли;	
		КонецЕсли;    
			
		Движение.Количество = КоличествоТовара;

	КонецЦикла;

	// Создание движения в регистре накопления Взаиморасчеты
	Движения.Взаиморасчеты.Записывать = Истина;
	Движение = Движения.Взаиморасчеты.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Поставщик;
	Движение.Валюта = Валюта;

	Если Валюта.Пустая() Тогда

		Движение.Сумма = Товары.Итог("Сумма");

	Иначе

		Курс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Валюта)).Курс;

		Если Курс = 0 Тогда
			Движение.Сумма = Товары.Итог("Сумма");
		Иначе
			Движение.Сумма = Товары.Итог("Сумма") / Курс;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда

		ЗапросПоКонтрагенту = Новый Запрос("ВЫБРАТЬ
		                                   |	Контрагенты.ЭтоГруппа
		                                   |ИЗ
		                                   |	Справочник.Контрагенты КАК Контрагенты
		                                   |ГДЕ
		                                   |	Контрагенты.Ссылка = &КонтрагентСсылка");
		ЗапросПоКонтрагенту.УстановитьПараметр("КонтрагентСсылка", ДанныеЗаполнения);
		Выборка = ЗапросПоКонтрагенту.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.ЭтоГруппа Тогда
			Возврат;
		КонецЕсли;
		
		Поставщик = ДанныеЗаполнения.Ссылка;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//Удалим из списка проверяемых реквизитов валюту, если по организации не ведется 
	//валютный учет
	Если НЕ ПолучитьФункциональнуюОпцию("ВалютныйУчет", Новый Структура("Организация", Организация)) Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Валюта"));
	КонецЕсли;	
	
КонецПроцедуры

Функция КоэффициентПерерасчетаЕдиницыИзмеренияНоменклатуры(Номенклатура, ЕдиницаИзмерения)
	
	ВозвращаемоеЗначение = 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыАльтернативныеЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.Товары.АльтернативныеЕдиницыИзмерения КАК ТоварыАльтернативныеЕдиницыИзмерения
	|ГДЕ
	|	ТоварыАльтернативныеЕдиницыИзмерения.Ссылка = &Номенклатура
	|	И ТоварыАльтернативныеЕдиницыИзмерения.ЕдиницаИзмерения = &ЕдиницаИзмерения";
	
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ВозвращаемоеЗначение = Выборка.Коэффициент;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение; 
	
КонецФункции	
